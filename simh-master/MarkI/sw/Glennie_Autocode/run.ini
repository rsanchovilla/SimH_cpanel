
  set console -n -q log=console.txt 
; set debug -n -q debug.txt

; set cpu debug=CMD;DATA;DETAIL;EXP

echo
echo ***
echo *** Run A.Glennie Autocode (1952)
echo ***
echo *** Type in the autocode programme on the Offline  
echo *** Perforator and get the punched PaperTape 
echo ***
echo

att -q -n Perforator Tapes/GAutoCode_prog.pt.txt

typein GAutocode source  %1
if "%STATUS%">"0" goto syntaxerr

det -q Perforator

echo
echo
echo ***
echo *** Run using Autocode Compiler

if "%1"=="" goto nofile
if not exist "%1" goto notfound
echo *** Programme "%1"

set env inputtape=0
if "%2"=="" goto endinp1
if "%2"=="." goto endinp1
if not exist "%2" goto notfound2
set env inputtape=1
echo *** Input tape "%2"
:endinp1

echo ***
echo

att -q drum GAutocode.drm
set drum nopersistent

set cpu KEC
; Hand switches must all be set to F/@/ in order to load Autocode from drum
dep H F/@/

if "%3"=="DEBUG" set debug -n -q debug.txt

; attach punch out tape
att ptp -q -n Tapes/Output.pt.txt

att -q ptr Tapes/GAutoCode_prog.pt.txt
set ptr echo

go

if "%inputtape%"=="0" goto endinp2
att -q ptr %2
go
:endinp2

det -q ptr
det -q ptp
goto end


:nofile
echo
echo Missing file.ga.txt as autocode program to run
echo
goto end

:notfound
echo
echo File "%1" not found
echo
goto end

:notfound2
echo
echo File "%2" not found
echo
goto end

:syntaxerr
echo Error in routine. Cannot Run
echo 
goto end

:end
