
set console -n -q log=console.txt 
; set debug -n -q debug.txt

set cpu debug=CMD;DATA;DETAIL;EXP

; parameter
; %1 -> the routine to run
; %2 -> the steering tape

echo
echo ***
echo *** Run using Scheme B 

set env WorkinPERM=0
if "%1"!="WKPERM" goto endWP
set env WorkinPERM=1
shift
:endWP

set env routine=0
if "%1"=="" goto nofile
if "%1"=="." goto endrou
if not exist "%1" goto notfound
set env routine=1
echo *** Routine "%1"
:endrou

set env steering=0
if "%2"=="" goto endste
if "%2"=="." goto endste
if not exist "%2" goto notfound2
set env steering=1
echo *** Steering tape "%2"
:endste

if "%routine%"=="0" goto endrtin1
echo
echo ***
echo *** Type in the routine code on the Offline Perforator and get 
echo *** it punched on PaperTape 
echo ***
echo

; prepare a routine to be loaded later at a given drum track
; use the offline Perforator to punch the routine in paper tape
; setup Perforator settings
set Perforator echo
set Perforator use_wc_k

; attach papertape to punch
att -q -n Perforator Tapes/Routine_tr10.pt.txt

; leave some blank tape at the beginning, mark start of tape with CCCC for
; easy visual location
typein line    //////CCCCCC//////

; punch destination addr of routine: track 10 (char R) Left and Right
typein line    K /# @ //// R/@/        
typein line    YTP

; proper punch. Will load the routine in S4 using warning chars K
typein routine1 %1 
if "%STATUS%">"0" goto syntaxerr
; now prepare routine 2 with columns @A
; Will load the routine in S5 using warning chars K
typein routine2 %1 S5
if "%STATUS%">"0" goto syntaxerr

; then save routine to destination track set in directory entry //
typein line    Y//
; call Z warning char so computer will stop if /G switch is on. Press prepulse key to go on
typein line    Z
; leave some blank tape at the end
typein line    $$$$$$//////
; detach paper tape from Perforator
det Perforator 
:endrtin1

echo
echo
echo ***
echo *** Start Computer  
echo ***

; set ptr echo

att -q drum SchemeB.drm
set drum NoPersistent

set cpu KEC
; Hand switches allow to work usign SchemeA PERM (H set to ////), SchemeB PERM
; (H set to /A@/ = 2144 in decimal), or SchemeB WORKING PERM (/A@E = 34912 in decimal)
; Regular Routines neededs Regular SchemeB PERM, FLOATCODE needs WORKING PERM

dep H 2144
if "%WorkinPERM%"=="1" dep H 34912

; the computer is ready to run something

if "%routine%"=="0" goto endrtin2

echo
echo ***
echo *** Load the punched Routine into drum (track 10) 
echo ***

; attach routine papertape to load it on drum
att ptr -q Tapes/Routine_tr10.pt.txt
; start the computer -> will read and obey the punched tape
; the punched tape stores the routine in S4/S5 store, then calls
; the warning char Y to save it in drum
go -q
:endrtin2

; attach punch out tape
att ptp -q -n Tapes/Output.pt.txt


; is there a steering tape ?
if "%steering%"=="1" goto st1

echo
echo
echo ***
echo *** Run Routine 
echo ***

; no steering tape, so call routine from dir entry // (poitns to track 10LR)
; and start at address // (so first instr executed at address 0001)

; reset the computer
set cpu KEC

if "%3"=="DEBUG" set debug -n -q debug.txt

; call cue (i.e call routine, then return to input)
feedptr G//
go
goto end

:st1

echo
echo
echo ***
echo *** Run Steering tape
echo ***

; reset the computer
set cpu KEC
go -q

if "%3"=="DEBUG" set debug -n -q debug.txt
;XXX if "%3"=="DEBUG" set ptr echo

; there is a steering tape. Attach it and continue
att -q ptr %2
go

det -q ptr
det -q ptp
goto end


:nofile
echo
echo Missing file.txt as parameter of script
echo
goto end

:notfound
echo
echo File "%1" not found
echo
goto end

:notfound2
echo
echo File "%2" not found
echo
goto end

:syntaxerr
echo Error in routine. Cannot Run
echo 
goto end

:end