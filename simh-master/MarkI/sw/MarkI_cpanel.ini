
; set console -n -q log=console.txt 
; set debug -n -q debug.txt

if "%CPANEL%"=="1" goto chk_cp_ok1

echo
echo ERROR!
echo markI compiled without control panel support. Build again 
echo with CPANEL, USE_SIM_VIDEO and HAVE_LIBSDL symbols defined 
echo
goto end

:chk_cp_ok1

if exist ../cpanel/MarkI.ini goto chk_cp_ok2

echo
echo ERROR!
echo cannot find the control panel definition file
echo "../cpanel/MarkI.ini" 
echo
goto end

:chk_cp_ok2

; start control panel

echo *** 
echo *** Start Control Panel GUI
echo *** 
echo *** HotKeys on Control Panel window:
echo ***                 +/- Zoom In/Zoom Out (also ^+ and ^-)
echo ***                 ^Y  Toggle zoom 100%% <-> 50%%
echo ***                 ^T  Toggle flash on clickable controls
echo ***                 ^I  Toggle info panel      
echo ***                 ^F  Fast mode: while pressed, accelerates cpu to max speed
echo *** 
echo ***           Click on control panel image and drag mouse to move the window.
echo ***           Right click mouse to show a tooltip with image at 100% scale.
echo ***           Keys A..Z, 0..9 can be used as keyboard printer keys if selected
echo *** 

att cpanel ../cpanel/MarkI.ini

; Parse DARK parameter. DARK makes SimH to run with set cpanel option=DarkMode
if "%1"!="DARK" goto enddark
   set cpanel option=Dark
   shift
:enddark

; optionaly before first param we can place DARK 
; first parameter is the cpu type: Simplified, Console, All, BAutocode. Big
; second parameter is the working directory

:cpu0
if "%1"!="Simplified" goto cpu1
   set cpanel option=CpuType/%1
   set cpanel scale=100
   goto cpuend
:cpu1
if "%1"!="Console" goto cpu2
   set cpanel option=CpuType/%1
   set cpanel scale=50
   goto cpuend
:cpu2
if "%1"!="All" goto cpu3
   set cpanel option=CpuType/%1
   set cpanel scale=25
   goto cpuend
:cpu3
if "%1"!="BAutocode" goto cpu4
   set cpanel option=CpuType/All
   set cpanel option=Perforator/Creed7
   set cpanel scale=25
   goto cpuend
:cpu4
if "%1"!="Big" goto cpu5
   set cpanel option=CpuType/All
   set cpanel scale=50
   goto cpuend
:cpu5
   echo Unknown cpu type "%1", using default Console
   set cpanel option=CpuType/Console
   set cpanel scale=50
   goto cpuend

:cpuend

set cpu speed=1.0

set cpanel pos=150/30
set cpanel option=ShowInfo
set cpanel on

; second parameter is the working directory
if "%2"=="" goto end
cd %2

; execute script given as parameter
if "%3"=="" goto end

do %3 %4 %5 %6 %7 %8 %9


:end