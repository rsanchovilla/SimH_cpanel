
set console -n -q log=log.txt 

set -q nodebug 

if "%CPANEL%"=="1" goto chk_cp_ok1

echo
echo ERROR!
echo NORC Simulator compiled without control panel support. Build again 
echo with CPANEL, USE_SIM_VIDEO and HAVE_LIBSDL symbols defined 
echo
goto end

:chk_cp_ok1

if exist ../cpanel/NORC.ini goto chk_cp_ok2

echo
echo ERROR!
echo cannot find the control panel definition file
echo "../cpanel/NORC.ini" 
echo
goto end

:chk_cp_ok2

; start control panel

echo *** 
echo *** Start Control Panel GUI
echo *** 
echo *** HotKeys on GUI: +/-  Zoom In/Zoom Out (also ^+ and ^-)
echo ***                 ^Y   Toggle zoom 100%% <-> 50%%
echo ***                 ^T   Toggle flash on clickable controls
echo ***                 ^I   Toggle info panel (readable at 100%% and 50%% zoom)      
echo ***                 ^F   Fast mode: while pressed, accelerates cpu to max speed
echo ***                 0-9/BackSpace
echo ***                      Console keyboard digit (operative when GUI has focus, 
echo ***                      cpu is stoped, keyboard entry switch set to reg1/reg2) 
echo *** 
echo ***           Click under tape window to open/close the tape cabinet and show 
echo ***           vaccum columns
echo ***
echo ***           Click on control panel image and drag mouse to move the window
echo ***           Right click mouse to show a tooltip with image at 100% scale
echo *** 

att cpanel ../cpanel/NORC.ini
set cpanel option=ShowInfo
set cpanel option=AllPanels
set cpanel scale=50
set cpanel pos=170/70
set cpanel on

echo 
echo *** Run pert with        card_activity_input_big.txt   (holds 1095 events)
echo ***                      date 01 (month) 13 (day) 58 (year)

set cpu speed=1.0

cd PERT
do PERT_Run_Tape.ini    card_activity_input_big.txt    01 13 58   
cd ..

goto end


:end
