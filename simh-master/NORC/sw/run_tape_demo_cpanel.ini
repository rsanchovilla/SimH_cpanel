

set -q nodebug 

set cpu speed=1.0

if "%CPANEL%"=="1" goto chk_cp_ok1

echo
echo ERROR!
echo NORC Simulator compiled without control panel support. Build again 
echo with CPANEL, USE_SIM_VIDEO and HAVE_LIBSDL symbols defined 
echo
goto end

:chk_cp_ok1

if exist ../cpanel/NORC.ini goto chk_cp_ok2

echo
echo ERROR!
echo cannot find the control panel definition file
echo "../cpanel/NORC.ini" 
echo
goto end

:chk_cp_ok2

; start control panel

echo *** 
echo *** Start Control Panel GUI
echo *** 
echo *** HotKeys on GUI: +/-  Zoom In/Zoom Out (also ^+ and ^-)
echo ***                 ^Y   Toggle zoom 100%% <-> 50%%
echo ***                 ^T   Toggle flash on clickable controls
echo ***                 ^I   Toggle info panel (readable at 100%% and 50%% zoom)      
echo ***                 ^F   Fast mode: while pressed, accelerates cpu to max speed
echo ***                 0-9/BackSpace
echo ***                      Console keyboard digit (operative when GUI has focus, 
echo ***                      cpu is stoped, keyboard entry switch set to reg1/reg2) 
echo *** 
echo ***           Click under tape window to open/close the tape cabinet and show 
echo ***           vaccum columns
echo ***
echo ***           Click on control panel image and drag mouse to move the window
echo ***           Right click mouse to show a tooltip with image at 100% scale
echo *** 

att cpanel ../cpanel/NORC.ini
SET cpanel option=ShowInfo
SET cpanel option=AllPanels
SET cpanel scale=50
set cpanel pos=-200/50
set cpanel on

SET CPANEL OPTION=MT1/V*R
at -q mt1 PERT\build\PERT_program.tap
SET CPANEL OPTION=MT2/V*F
at -q mt2 PERT\build\PERT_program.tap
SET CPANEL OPTION=MT3/V
at -q mt3 PERT\build\PERT_program.tap
SET CPANEL OPTION=MT4/C
at -q mt4 PERT\build\PERT_program.tap
SET CPANEL OPTION=MT5/C
at -q mt5 PERT\build\PERT_program.tap

echo 
echo *** Infinite loop, count on REG1
echo ***                 


dep 1000 00 60 1010 0000 0000
dep 1001 00 40 0000 1011 0000
dep 1002 00 60 0000 0000 1001
dep 1010 0
dep 1011 1

dep v 1000
go


goto end


:end
